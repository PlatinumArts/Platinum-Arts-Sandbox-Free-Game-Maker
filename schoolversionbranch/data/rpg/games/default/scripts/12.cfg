//Ahriman the magic guy

include scripts/1

r_script_node  "main" [result "Oh my, a visitor! Greetings, make yourself at home."] [
	ifdumb player [
		ifbit $ahrimanflags 1 [
			r_response "Me go before head hurt more." ""
		] [
			//the simple minded cannot learn about the secret areas
			r_response "What magic man do?" "intro" [ onbit $ahrimanflags 1 ]
			r_response "Me go now." ""
		]
	] [
		ifbit $ahrimanflags 1 [
			ifbit $ahrimanflags 2 [
				r_response "Would you like me to look into the delayed lanterns for you?" "TODO"
			] [
				r_response "Have you invented anything recently?" "invention" [ onbit $ahrimanflags 2 ]
			]
			ifbit $ahrimanflags 4 [
				ifbit $teleportflags 2 [] [
					r_response "How would I get to ^"God's Testing Hall^"?" "hallportal" [onbit $teleportflags 2]
				]
			] [
				r_response "Have you learned any secrets of the world?" "discovery" [onbit $ahrimanflags 4]
			]
		] [
			r_response "Are you a magician by chance?" "intro" [ onbit $ahrimanflags 1 ]
		]
		r_response "Farewell." ""
	]
]

r_script_node "intro" [result "[You think you heard him scoff] I am not a mere magician, I am an artificer. I delve the intricacies of reality and the ethereal to further our study of science and the arcane!"] [
	ifdumb player [
		r_response "Beeg words, brain hurt, me go." ""
	] [
		r_response "Fascinating, make any recent technological discoveries?" "invention" [ onbit $ahrimanflags 2 ]
		r_response "Interesting, what have you learned about the realm of magic?" "discovery" [onbit $ahrimanflags 4]
		r_response "I'll return later, farewell." ""
	]
]

r_script_node "invention" [result "Why yes, I recently drew up plans for a specialised light emitting container. I have decided to call it a ^"lantern.^" [He points towards the magic circle in the centre of the room] He suggested the name. [He then proceeds to describe the creation in utmost detail with overwhelming excitement, often comparing it favourably to candles, torches and braziers in his descriptions]"]  [
	r_response "I don't suppose you've made a few?" "commission"
]

r_script_node "commission" [result "I've commissioned a few from a Blacksmith outside the valley a while back, alas they've not arrived. [some quest details pending]"] [
	r_response "Want me to look into it for you?" "TODO"
	ifbit $ahrimanflags 4 [] [
		r_response "Interesting, what have you learned about the realm of magic?" "discovery" [onbit $ahrimanflags 4]
	]
	r_response "Farewell" ""
]

r_script_node "discovery" [result "I did stumble among the most curious thing the other day, but first. You seem to be a well educated man, so tell me... Have you ever created your own personal little world? Whether that be for art, literature or any other purpose."] [
	r_response "I have, why?" "world"
]

r_script_node "world" [result "Have you exerted any effort to make sure that your little world is logical in its foundation?"] [
	r_response "That is a task upon itself..." "hall"
	r_response "Can't say that I have, no" "hall"
]

r_script_node "hall" [result "[He is absolutely beaming with pride] The idea just occurred to me, if our world has a creator, what if they used the exact same process we did? 'Lo behold, that's when I stumbled upon this other world I call ^"God's Testing Hall.^" Why such a name you may ask? The layout of the world is essentially and endless corridor with doors leading to the various experiments of our creators. It's the place they honed their skills before making ours."] [
	r_response "How would I get to this place?" "hallportal" [onbit $teleportflags 2]
	ifbit $ahrimanflags 2 [] [
		r_response "Fascinating, make any recent technological discoveries?" "invention" [ onbit $ahrimanflags 2 ]
	]
	r_response "Farewell." ""
]

r_script_node "hallportal" [result "Quite simple really. Hold the name with your mind and walk into a teleporter. You can find one near the base of my tower. Be careful though, many areas will not have a nearby portal."] [
	r_response "Thank you, farewell" ""
]
