// The Baker

//TODO allow the player to coax another pie out of hte baker by convincing him that he forgot to give you a pie.
//  doing it a second time will make ihm go hostile

include "includes/valleyvillager script"

r_script_signal_append "spawn" [
	r_additem self 0 (rnd 100 50) //some extra gold
	r_additem self 2 4 //some pies
]

//changes dialogue, can be overriden in other files witohut consequence
giveapples = [
	if (= (r_get_amount player 1) 0) [
		r_chat talker "noapples"
	] [
		amnt = (min (- 15 (r_get_amount talker 1)) (r_get_amount player 1))
		r_remove player 1 $amnt //remove apples from inventory
		r_additem talker 1 $amnt //places apples in talker's inventory

		if (= (r_get_amount talker 1) 15) [
			r_global_set $applequest 2
			r_chat talker "gotapples"
		] [
			r_chat talker "gotsomeapples"
		]
	]
]

r_script_node "main" [result "Why hello there!^nWhat brings you to my humble little bakery today?"] [ // 0
	ifdumb player [
		case (r_global_get $applequest) 0 [
			r_response "Me want apple pie, you give!" "getapplequest" [
				r_global_set $applequest 1
			]
		] 1 [
			r_response "Me bring ape-pulls!" "" [ @@@@giveapples ]
		] 2 [
			r_response "Pie! Give pie!" "givepie" [
				r_global_set $applequest 3
				r_additem player 2 1
			]
		]
		r_response "Tuh-raid?" "" [ r_trade talker ]
		r_response "You have answers?" "inquire"
		r_response "Bubbii!" ""
	] [
		case (r_global_get $applequest) 0 [
			r_response "Hi! I'd like a *delicious* apple pie." "getapplequest" [
				r_global_set $applequest 1
			]
		] 1 [
			r_response "I've brought you some apples." "" [ @@@@giveapples ]
		] 2 [
			r_response "I've come to pick up my pie!" "givepie" [
				r_global_set $applequest 3
				r_additem player 2 1
			]
		] // 3 already has the reward - maybe add some sweet talking moments?
		r_response "Let's barter" "" [ r_trade talker ]
		r_response "Mind answering a few questions for me?" "inquire"
		r_response "Good-bye!" ""
	]
]

r_script_node "inquire" [ at ["Hmm?" "Yes?" "Sure" "What do you want to know?"] (rnd 4) ] [ // 1
	ifdumb player [
		if (r_global_get $applequest) [
			r_response "Me want do work!" "hasapplequest"
		] [
			r_response "Me want do work!" "getapplequest2" [
				r_global_set $applequest 1
			]
		]
		if (= (r_global_get $applequest) 1) [
			r_response "Me no find apples, help!" "apples"
		]
		r_response "Me axe-plo-ruh, you know good place?" "dumbarea"
		r_response "Buy-buy!" ""
	] [
		if (r_global_get $applequest) [
			r_response "Do you have any more work for me?" "hasapplequest"
		] [
			r_response "Do you have any work for me?" "getapplequest2" [
				r_global_set $applequest 1
			]
		]
		if (= (r_global_get $applequest) 1) [
			r_response "I'm having some trouble tracking down apples, any advice?" "apples"
		]
		r_response "Know of any interesting areas nearby?" "area"
		r_response "I noticed everyone else has first and last names starting with the same letter." "alliteratednames"
		r_response "Nothing, farewell!" ""
	]
]

r_script_node "dumbarea" [result "Look around the steep rock walls that border this valley, you should be able to find a few good spots around there. There's a wizard outside the town as well, but I don't think you'll gain much from him my simple minded friend."] [ // dumb only
	r_response "Tank you, more question." "inquire"
	r_response "Tank you, me go see!" ""
]

r_script_node "area" [result "As you're probably already aware, we're in a valley surrounded by steep cliff faces. I'd look around these cliffs for anything interesting, such as the waterfall to the north east. You will also find a large tower to the north, it'll be worth your while to pay a visit."] [ // !dumb only
	r_response "Thank you, a few more questions." "inquire"
	r_response "I'll check it out, thanks." ""
]

r_script_node "getapplequest" [result "I'm out of apple pies and apples at the moment. If you can bring me 15 apples, I'll accept that as payment for a pie."] [ // 4
	ifdumb player [
		r_response "Where find appuls?" "apples"
		if (r_get_amount player 1) [
			r_response "I has appuls!" "" [ @@@@giveapples ]
		]
		r_response "Me go find!" ""
	] [
		r_response "Any idea where I could find a few?" "apples"
		if (r_get_amount player 1) [
			r_response "I have a few apples already, here." "" [ @@@@giveapples ]
		]
		r_response "I'll be right back with the apples" ""
	]
]

r_script_node "getapplequest2" [result "You're looking for work? Now that I think about it, I've run out of fresh apples, If you could bring me 15 or so, I'll bake you a pie for your efforts."] [ // 5
	ifdumb player [
		r_response "Where find?" "apples"
		if (r_get_amount player 1) [
			r_response "I has appuls!" "" [ @@@@giveapples ]
		]
		r_response "Me go find!" ""
	] [
		r_response "Any tips as to where I can find them?" "apples"
		if (r_get_amount player 1) [
			r_response "I have a few apples already, here." "" [ @@@@giveapples ]
		]
		r_response "I'll be back." ""
	]
]

r_script_node "hasapplequest" [result "All I require for now are the apples I've already asked you to bring me."] [ // 6
	ifdumb player [
		r_response "More answers?" "inquire"
		r_response "Ugh... Byebye." ""
	] [
		r_response "A few more questions if i may." "inquire"
		r_response "I see. Farewell" ""
	]
]

r_script_node "noapples" [result "May I have them then?"] [ //7
	ifdumb player [
		r_response "Me get you! Ha! ha! Me not have any." "noapples2"
		r_response "Ugh... me not find, be back." ""
	] [
		r_response "[Search your inventory] Er... I must've dropped them." ""
	]
]

r_script_node "noapples2" [result "[He simply glares at you, you suspect he isn't quite as amused as you are]"] [ //8 - dumb only
	r_response "Ugh... Me go find then." ""
]

r_script_node "gotsomeapples" [result "Thank you, I'll keep a hold of them until you bring me the rest"] [ //9
	ifdumb player [
		r_response (concat "Okee dokee, me go find" (rnd 15 1) "more.") "" //can't help it, sorry :P
	] [
		r_response (concat (- 15 (r_get_amount talker 1)) "more to go, I'll return after I've found them.") ""
	]
]

r_script_node "gotapples" [result "And that makes 15 apples you've brought me. I'll make the filling for the pies right away, come back in a little while [He turns away from you and prepares the filling for several unfinished pie shaped pastries]."] [ //10
	r_response "[Leave]" ""
]

r_script_node "apples" [result "Apples? I'm not too sure, I don't think they grow around here. I buy most of my ingredients from Gracy Grocer so she may be a better person to ask."] [ //11
	ifdumb player [
		r_response "Thankee, me need more answers." "inquire"
		r_response "Me go talk to Grotty Grocy, byebye." ""
	] [
		r_response "Thank you, a few more questions if I may." "inquire"
		r_response "I shall pay her a visit then, farewell for now." ""
	]
]

r_script_node "givepie" [result "[He picks up a pie from near the back of the room and passes it to you] Here you go, one freshly baked Danish Apple Pie. It just came out so it's still pretty hot. Take care now"] [ //12
	ifdumb player [
		r_response "[You drool for a moment and accept the pie] Thankee." ""
	] [
		r_response "[You accept the pie graciously] Pleasure doing business." ""
	]
]

r_script_node "alliteratednames" [result "Now that you mention it, that is quite strange. It seems I and the wizard are the only exceptions. I'm honestly not sure why I'm not part of this little tradition."] [ //13 !dumb only
	if (r_global_get $ahrimanflags) [
		r_response "Ahriman's an *artificer*" "artificer"
	]
	r_response "A few more questions if I may" "inquire"
	r_response "Farewell" ""
]

r_script_node "artificer" [result "Whatever..."] [ //14 !dumb only
	r_response "Er... A few more questions if I may" "inquire"
	r_response "Er... Farewell for now" ""
]
