newentgui creature "yaw type tag" "0 360 0 6 -1000 1000
newentgui teleport "tag model" "0 20 -1 98"
newentgui teledest "direction tag" "0 360 0 20"
newentgui playerstart "direction team" "0 360 0 2"
newentgui jumppad "Z Y X" "-200 200 -200 200 -200 200"
newentgui carrot "tag type" "0 50 0 29"
newentgui elevator "direction mapmodel tag speed" "0 360 0 (nummapmodels) 0 30 0 100"
newentgui barrel "direction mapmodel weight" "0 360 0 (nummapmodels) -100 100"
newentgui box "direction mapmodel weight" "0 360 0 (nummapmodels) -100 100"
newentgui platform "direction mapmodel tag speed" "0 360 0 (nummapmodels) 0 30 0 100"

crosshairs = "default teammate hit edit"
filters = [
	@filters
	"chat"
	"team chat"
	"game events"
	"frags"
	"opponent frags"
	"team kills"
]

maprotation = [
	n = (listlen $arg1)
	loop i $n [
		curmap = (at $arg1 $i)
		next = (mod (+ $i 1) $n)
		nextmap = (at $arg1 $next)
		(if (= $numargs 2) [concatword nextmap_ $arg2 $curmap] [concatword nextmap_ $curmap]) = $nextmap
	]
]

spmaps = [ "savetheprincess" "savetheprincess2" "village" "experimentalchambers" ]
ffamaps = [ "diamond" "Snowcourt" "spacectf" ]
capturemaps = [ "savethebanana" ]
ctfmaps = [ "Snowcourt" "spacectf" ]

maprotation $ffamaps
maprotation $capturemaps capture_
maprotation $ctfmaps ctf_

spmodes = [ -2 -3 ]
ffamodes = [ 0 1 4 5 6 ]
capturemodes = [ 2 ]
ctfmodes = [ 3 ]

spmode = -2
ffamode = 0
capturemode = 2
ctfmode = 3

//args Name, mode var, modestring, map string
GenMapModeList = [
	uitext $arg1 1.2
	uicolor 1 1 1 .1 0 0 [
		uialign 0 0
		uiclamp 1 1 0 0
		uitable 3 .01 [
			looplist g $arg3 [
				UIRadioButton (format "%1 (%2)" (modename $g) $g)  $arg2 $g () [uialign -1 0]
			]
		]
	]
	uitable 6 .01 [
		looplist m $arg4 [
			UIImageButton .135 .135 [
				mode $@@arg2
				map (concatword "fps/" [@@m])
			] [
				uiimage (concatword "packages/base/fps/" $m) .125 .125 [
					uialtimage "data/sandboxlogo"
				]
			] $m
		]
	]
]

LoadMapSelections = [
	replaceui [@arg1] maps [
		uivlist 0.005 [
			uialign -1 0
			GenMapModeList "Singleplayer" spmode $spmodes $spmaps
			uifill 0 .015
			GenMapModeList "Free For All" ffamode $ffamodes $ffamaps
			uifill 0 .015
			GenMapModeList "Capture/Domination" capturemode $capturemodes $capturemaps
			uifill 0 .015
			GenMapModeList "Capture The Flag" ctfmode $ctfmodes $ctfmaps
		]
	]
]


UIOverlay "scores" [
	local clients spectators player

	player = (at (listclients 1) 0)
	if (>= (getfollow) 0) [
		player = (getfollow)
	]

	looplist c (listclients 1) [
		if (isspectator $c) [
			spectators = (concat $spectators $c)
		] [
			clients = (concat $clients $c)
		]
	]
	uispace .02 .02 [
		uivlist 0.02 [
			uifill 0 0 [
				uiclamp 1 1
				uitext (modename (getmode)) 0 0 [uialign -1 0]
				uievaltext [timeremaining 1] 0 0 [uialign 1 0]
			]
			if (m_teammode (getmode)) [
				local teams
				teams = ""

				looplist c $clients [
					if (= (indexof $teams (getclientteam $c)) -1) [
						teams = (concat $teams (escape (getclientteam $c)))
					]
				]

				uitable 2 .02 [
					looplist t $teams [
						local r g b

						if (&& (! (isspectator $player)) (strcmp (getteam) (unescape $t))) [
							r = .2; g = .3; b = 1;
						] [
							r = 1; g = .3; b = .2;
						]

						uicolor $r $g $b .5 0 0 [
							uiclamp 1 1 1 1
							uivlist 0.01 [
								uialign -1 -1
								uitext (concat (unescape $t) - (getteamscore (unescape $t))) 1 -1 [ uialign -1 -1 ]

								uitable 8 0.01 [
									uialign -1 -1

									uitext "cn" 0 0 [uialign -1 0]
									uifill 0 0
									uitext "Name" 0 0 [uialign -1 0]
									uitext "Ping" 0 0 [uialign 1 0]
									uitext "PJ" 0 0 [uialign 1 0]
									uitext "Frags" 0 0 [uialign 1 0]
									uitext "Flags" 0 0 [uialign 1 0]
									uitext "Acc." 0 0 [uialign 1 0]

									looplist c $clients  [
										if (strcmp (unescape $t) (getclientteam $c)) [
											local r g b
											cond [ isadmin $c ] [
												r = 1; g = .5; b = 0
											] [ ismaster $c ] [
												r = .25; g = 1; b = .5
											] [ 1 ] [
												r = 1; g = 1; b = 1;
											]
											if (isdead $c) [
												r = (divf $r 2)
												g = (divf $g 2)
												b = (divf $b 2)
											]

											uicolortext $c 1 -1 $r $g $b
											uiimage (concatword "data/newui/" (getclienticon $c)) .025 .025
											uicolortext (getclientname $c) 1 -1 $r $g $b [uialign -1 0]
											uicolortext (getclientping $c) 1 -1 $r $g $b [uialign 1 0]
											uicolortext (getclientpj $c) 1 -1 $r $g $b [uialign 1 0]
											uicolortext (getclientfrags $c) 1 -1 $r $g $b [uialign 1 0]
											uicolortext (getclientflags $c) 1 -1 $r $g $b [uialign 1 0]
											uicolortext (getclientaccuracy $c) 1 -1 $r $g $b [uialign 1 0]
										]
									]
								]
							]
						]
					]
				]
			] [
				//do we need 'flags' for collect?
				uitable 7 0.01 [
					uialign -1 -1
					uitext "cn" 0 0 [uialign -1 0]
					uifill 0 0
					uitext "Name" 0 0 [uialign -1 0]
					uitext "Ping" 0 0 [uialign 1 0]
					uitext "PJ" 0 0 [uialign 1 0]
					uitext "Frags" 0 0 [uialign -1 0]
					//uitext "Flags" 0 0 [uialign -1 0]
					uitext "Acc." 0 0 [uialign -1 0]
					looplist c $clients [
						local r g b
						cond [ isadmin $c ] [
							r = 1; g = .5; b = 0
						] [ ismaster $c ] [
							r = .25; g = 1; b = .5
						] [ 1 ] [
							r = 1; g = 1; b = 1;
						]
						if (isdead $c) [
							r = (divf $r 2)
							g = (divf $g 2)
							b = (divf $b 2)
						]

						uicolortext $c 1 -1 $r $g $b [uialign -1 0]
						uiimage (concatword "data/newui/" (getclienticon $c)) .025 .025
						uicolortext (getclientname $c) 1 -1 $r $g $b [uialign 1 0]
						uicolortext (getclientping $c) 1 -1 $r $g $b [uialign 1 0]
						uicolortext (getclientpj $c) 1 -1 $r $g $b [uialign 1 0]
						uicolortext (getclientfrags $c) 1 -1 $r $g $b [uialign 1 0]
						//uicolortext (getclientflags $c) 1 -1 $r $g $b [uialign 1 0]
						uicolortext (getclientaccuracy $c) 1 -1 $r $g $b [uialign 1 0]
					]
				]
			]

			if (listlen $spectators) [
				uitable 5 0.01 [
					uialign -1 -1
					uitext "cn" [uialign -1 0]
					uifill 0 0
					uitext "Name" [uialign -1 0]
					uitext "Ping" [uialign 1 0]
					uitext "PJ" [uialign 1 0]

					looplist c $spectators [
						local r g b
						cond [ isadmin $c ] [
							r = 1; g = .5; b = 0
						] [ ismaster $c ] [
							r = .25; g = 1; b = .5
						] [ 1 ] [
							r = 1; g = 1; b = 1;
						]

						uicolortext $c 1 -1 $r $g $b [uialign -1 0]
						uiimage (concatword "data/newui/" (getclienticon $c)) .025 .025
						uicolortext (getclientname $c) 1 -1 $r $g $b [uialign -1 0]
						uicolortext (getclientping $c) 1 -1 $r $g $b [uialign 1 0]
						uicolortext (getclientpj $c) 1 -1 $r $g $b [uialign 1 0]
					]
				]
			]
		]
	]
]

multimenu = 0

LoadMultiplayer = [
	replaceui [@arg1] title [
		uitext "Main Menu - Multiplayer" 1.5
	]
	replaceui [@arg1] content [
		uivlist 0 [
			uifill 0 0
			uialign -1 -1
			uihlist 0.01 [
				UITab multimenu 0 "Servers" [
					LoadMultiServers [@@@@@arg1]
				]
				UITab multimenu 1 "Player" [
					LoadMultiPlayer [@@@@@arg1]
				]
				UITab multimenu 2 "Mastermode" [
					LoadMultiMaster [@@@@@arg1]
				]
			]
			uitag body []
		]
	]
	multimenu = 0
	LoadMultiServers [@arg1]
]

// general server info atributes
// 0 - Name/Host
// 1 - Port
// 2 - Description
// 3 - Map
// 4 - Ping
// 5 - Number of Players

// FPS auxilary information
// 0 - protocol version
// 1 - game mode
// 2 - seconds remaining
// 3 - max players
// 4 - mastermode

LoadMultiServers = [
	replaceui [@arg1] body [
		uivlist 0 [
			UIButton "Refresh list" [ updatefrommaster; LoadMultiServers [@@@@@arg1] ]
			UIVScroller .9 .71 0.025 .5 [
				uispace 0.01 0.01 [
					uiclamp 1 1 1 1
					uitable 6 .01 [
						uialign -1 -1
						uiclamp 1 1 0 0
						loop i (numservers) [
							if (mod $i 15) [] [
								if $i [loop j 6 [uifill 0 0] ] //alignment
								uitext "Name" 0 0 [uialign -1 0]
								uitext "Port" 0 0 [uialign -1 0]
								uitext "Description" 0 0 [uialign -1 0]
								uitext "Map" 0 0 [uialign -1 0]
								uitext "Ping" 0 0 [uialign 1 0]
								uitext "Players" 0 0 [uialign 1 0]
							]

							UIBasicButton [
								uievaltext [ serverinfo @@i 0 ]
							] [ selectserver @i; connectselected ] [
								uialign -1 0
							]
							UIBasicButton [
								uievaltext [ serverinfo @@i 1 ]
							] [ selectserver @i; connectselected ] [
								uialign -1 0
							]
							UIBasicButton [
								uievaltext [ serverinfo @@i 2 ]
							] [ selectserver @i; connectselected ] [
								uialign -1 0
							]
							UIBasicButton [
								uievaltext [ serverinfo @@i 3 ]
							] [ selectserver @i; connectselected ] [
								uialign -1 0
							]
							UIBasicButton [
								uievaltext [ serverinfo @@i 4 ]
							] [ selectserver @i; connectselected ] [
								uialign 1 0
							]
							UIBasicButton [
								uievaltext [ concatword (serverinfo @@i 5) / (serverauxinfo @@i 3) ]
							] [ selectserver @i; connectselected ] [
								uialign 1 0
							]
						]
					]
				]
			]
		]
	]
]

LoadMultiPlayer = [
	replaceui [@arg1] body [
		uifill .9 .75
		uivlist 0 [
			uihlist 0.01 [
				uialign -1 -1
				uitext "Name:"
				newname = (getname)
				UIField newname 18 [ name $newname; newname = (getname) ] 1 $UISafeFilter
			]
			uievaltext [concat "Current model:" $playermodel] 0 0 [uialign -1 -1]
			uifill 0 .03
			UIVScroller .9 .6 0.025 .5 [
				uivlist 0.01 [
					loop i 7 [
						UIBasicButton [
							uicolor .7 .7 .7 .7 .9 .12 [
								uihlist 0.02 [
									uialign -1 -1
									uicolor 1 0 0 1 .12 .12
									uitext (concat "Playermodel" @@@i) 2
								]
							]
						] [ playermodel @i]
					]
				]
			]
		]
	]
]

LoadMultiMaster = [
	replaceui [@arg1] body [
		uifill .9 .75
	]
]

LoadGameOptions = [
	replaceui [@arg1] options [
		uialign -1 -1
		uivlist 0.01 [
			uitext "Feedback" 1.25 0
			uioffset .02 0 [ uivlist 0.01 [
				UICheckBox "Hit Tone" hitsound 1 0
				UICheckBox "Score on death" deathscore 1 0
				//UICheckBox "Blood" blood 1 0 "" [uialign -1 0]
				UICheckBox "Muzzle Flash" muzzleflash 1 0
				UICheckBox "Muzzle Light" muzzlelight 1 0
				UICheckBox "Team Crosshair" teamcrosshair 1 0
				// hitcrosshair
				// maxdebris
				// maxbarreldebris
			]]
			uifill 0 0

			uitext "HUD" 1.25
			uioffset .02 0 [ uivlist 0.01 [
				UICheckBox "Ammo Display" ammohud 1 0
				//UICheckBox "Hudguns" hudgun 1 0
				UICheckBox "Hudgun Sway" hudgunsway 1 0
				UICheckBox "Team Hudguns" teamhudguns 1 0
				// minradarscale
				// maxradarscale
				// minimapalpha
				// chainsawhudgun
			]]
			uifill 0 0

			uitext "Ragdolls" 1.25
			uioffset .02 0 [ uivlist 0.01 [
				UICheckBox "Use Ragdolls" ragdoll 1 0
				// ragdollmillis
				// ragdollfade
				// deadpush
			]]
			uifill 0 0

			uitext "Capture" 1.25
			uioffset .02 0 [ uivlist 0.01 [
				UICheckBox "Draw Tether" capturetether 1 0
				UICheckBox "Automatic Ammo Pickup" autorepammo 1 0
				UICheckBox "Display Base Numbers" basenumbers 1 0
			]]
			uifill 0 0

			uitext "Misc" 1.25
			uioffset .02 0 [ uivlist 0.01 [
				UICheckBox "Auth Automatically" autoauth 1 0
				UICheckBox "Show Mode Information" showmodeinfo 1 0
				UICheckBox "Force Uniform Player Models" forceplayermodels 1 0
				UICheckBox "Always Use Team Skins" teamskins 1 0
				// mouselook
				// spawnwait
			]]
		]
	]
]
